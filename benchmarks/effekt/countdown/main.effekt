import immutable/list
import immutable/option
import io/args
import text/string

effect State {
  def get(): Int
  def set(i: Int): Unit
}

def countdown(): Int / State = {
  val i = do get()
  if (i == 0) {
    i
  } else {
    do set(i - 1);
    countdown()
  }
}

def run(n: Int) = {
  var s = n;
  try {
    countdown()
  } with State {
    def get() = { resume(s) }
    def set(i: Int) = { s = i; resume(()) }
  }
}

def main() = {
  val n = commandLineArgs().headOption.getOrElse { "" }.toInt.getOrElse { 5 };
  val r = run(n);
  println(r)
}